apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 2
        versionName "0.3.1-1"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    implementation 'com.android.support:support-annotations:28.0.0'
    implementation 'com.couchbase.lite:couchbase-lite-android:2.1.0'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.1.0'
    testImplementation 'junit:junit:4.12'
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'io.sportner.cblmapper'
version = '0.3.1-1'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'CBLMapper'
            description 'https://github.com/sportner/CBLMapper'
            url 'https://github.com/sportner/CBLMapper'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'io.sportner.cblmapper'
            artifactId 'cblmapper'
            version '0.3.1-1'

            licenses {
                license {
                    name "The Apache Software License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                }
            }
            scm {
                connection 'https://github.com/sportner/CBLMapper.git'
                url 'https://github.com/sportner/CBLMapper'

            }
            developers {
                developer {
                    id = 'alblanc'
                    name 'Alban DEFLANDRE'
                    email 'alban@sportner.io'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'Android'
        name = 'CBLMapper'
        userOrg = 'sportner'
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = '0.3.1-1'
            desc = '0.3.1-1 Supports result (for query result mapping)'
            released = new Date()
            vcsTag = '0.3.1-1-alpha'
        }
    }
}