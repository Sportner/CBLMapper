apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion '26.0.2'

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 25
        versionCode 1
        versionName "0.2-DB08"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.couchbase.lite:couchbase-lite-android:2.0.0-DB020'
    testCompile 'junit:junit:4.12'
}


task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'io.sportner.cblmapper'
version = '0.2-DB08'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'CBLMapper'
            description 'https://github.com/sportner/CBLMapper'
            url 'https://github.com/sportner/CBLMapper'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'io.sportner.cblmapper'
            artifactId 'cblmapper'
            version '0.2-DB08'

            licenses {
                license {
                    name "The Apache Software License, Version 2.0"
                    url "http://www.apache.org/licenses/LICENSE-2.0.txt"
                }
            }
            scm {
                connection 'https://github.com/sportner/CBLMapper.git'
                url 'https://github.com/sportner/CBLMapper'

            }
            developers {
                developer {
                    id = 'alblanc'
                    name 'Alban DEFLANDRE'
                    email 'alban@sportner.io'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'Android'
        name = 'CBLMapper'
        userOrg = 'sportner'
        licenses = ['Apache-2.0']
        publish = true
        version {
            name = '0.2-DB08'
            desc = '0.1 not stable. Still experimental. List + Enum supports is OK. HashMap coming in the next build'
            released = new Date()
            vcsTag = '0.2-DB08'
        }
    }
}